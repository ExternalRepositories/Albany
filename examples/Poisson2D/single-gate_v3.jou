#     {Dsi = 100}
#     {Dsio2 = 60}
#     {Dal = 30}
#     {Dal2o3 = 20}
#     {Dg = 30}

#     {Woff = 100}
#     {Wg = 30}
#     {W = 2*Woff+Wg}

    
### -------- create vertices ---------------------------------------------- ###
# vertices for Silicon substrate
create vertex 0 0 0 
create vertex {Dsi} 0 0
create vertex {Dsi} {W} 0
create vertex 0 {W} 0

# vertices for SiO2 region
create vertex {Dsi+Dsio2} 0 0
create vertex {Dsi+Dsio2} {Woff} 0
create vertex {Dsi+Dsio2} {Woff+Wg} 0
create vertex {Dsi+Dsio2} {W} 0

# vertices for depletion gate region
create vertex {Dsi+Dsio2+Dg} {Woff} 0
create vertex {Dsi+Dsio2+Dg} {Woff+Wg} 0

# vertices for Al2O3 region
create vertex {Dsi+Dsio2+Dal2o3} 0 0
create vertex {Dsi+Dsio2+Dal2o3} {Woff-Dg} 0
create vertex {Dsi+Dsio2+Dal2o3+Dg} {Woff-Dg} 0
create vertex {Dsi+Dsio2+Dal2o3+Dg} {Woff+Wg+Dg} 0
create vertex {Dsi+Dsio2+Dal2o3} {Woff+Wg+Dg} 0
create vertex {Dsi+Dsio2+Dal2o3} {W} 0

# vertices for inversion gate
create vertex {Dsi+Dsio2+Dal2o3+Dg} 0 0
create vertex {Dsi+Dsio2+Dal2o3+Dg} {Woff-Dg-Dal} 0
create vertex {Dsi+Dsio2+Dal2o3+Dal+Dg} {Woff-Dg-Dal} 0
create vertex {Dsi+Dsio2+Dal2o3+Dal+Dg} {Woff+Wg+Dg+Dal} 0
create vertex {Dsi+Dsio2+Dal2o3+Dg} {Woff+Wg+Dg+Dal} 0
create vertex {Dsi+Dsio2+Dal2o3+Dg} {W} 0


### -------- create curves  ---------------------------------------------- ###
# curves for Silicon substrat
create curve vertex 1 2
create curve vertex 2 3
create curve vertex 3 4
create curve vertex 4 1

# curves for SiO2 region
create curve vertex 2 5
create curve vertex 5 6
create curve vertex 6 7
create curve vertex 7 8
create curve vertex 8 3

# curves for depletion gate region
create curve vertex 6 9
create curve vertex 9 10
create curve vertex 10 7

# curves for Al2O3 region
create curve vertex 5 11
create curve vertex 11 12
create curve vertex 12 13
create curve vertex 13 14
create curve vertex 14 15
create curve vertex 15 16
create curve vertex 16 8

# curves for inversion gate region
create curve vertex 11 17
create curve vertex 17 18
create curve vertex 18 19
create curve vertex 19 20
create curve vertex 20 21
create curve vertex 21 22
create curve vertex 22 16


### -------- create surfaces ---------------------------------------------- ###
# surface for Silicon substrate
create surface curve 1 2 3 4

# surface for SiO2 region
create surface curve 5 6 7 8 9 2

# surface for depletion gate region
create surface curve 7 10 11 12

# surface for Al2O3 region
create surface curve 13 14 15 16 17 18 19 8 12 11 10 6

# surface for inversion gate region
create surface curve 20 21 22 23 24 25 26 18 17 16 15 14


### -------- merge all overlapped curves and vertices  -------------------- ###
# this is necessary, otherwise, new curves and vertices are generated by Cubit
merge all


### -------- define nodesets for BCs -------------------------------------- ###
# set the depletion gate BC edge
nodeset 1 curve 10 11 12
nodeset 1 name "depGate"
color nodeset 1 blue

# set the inversion gate BC edge
nodeset 2 curve 21 22 23 24 25
nodeset 2 name "invGate" 
color nodeset 2 red

# set the substrate bottom
nodeset 3 curve 4
nodeset 3 name "substrate"
color nodeset 3 black


### -------- define blocks and mesh element type ---------------------------- ###
block 1 surface 1 
block 2 surface 2
block 3 surface 3
block 4 surface 4
block 5 surface 5
block 1 2 3 4 5 element type quad


### -------- create a group for easy mesh size specification ----------------- ###
group 1 add surface 1 to 5

# global mesh size
group 1 size 5

# mesh the structure
mesh group 1

# refine mesh in SiO2 near SiO2/Poly and SiO2/Al2O3 interfaces
# face IDs come from Cubit GUI
refine face 1381 to 1472 numsplit 1 bias 1.5 depth 1 smooth

# merge all overlapped meshing elements
merge all

# outputthe Exodus II mesh
export mesh "single-gate_v3.exo" dimension 2 block all overwrite
